# 武汉市地铁1号线汉西一路站自动售票系统的设计与仿真

## 一、设计思路

本设计用于模拟地铁售票机自动售票，完成地铁售票机的核心功能。

当乘客按下开始购票按钮进行购票，此时只能选择一个出站口，根据选站的不同票价不同。共设有27站(如图1-1所示)，票价参考表1-1。乘客购票时可以一次购买几张票，本设计中最多可以支持4张。售票机投币口识别范围为1元、10元。投币时可以连续投币，当投入金额大于所需时，出票并找零，结束本次交易，系统恢复初始状态等待下次购票。在购票过程中乘客可以在出票之前按取消按钮来取消操作，系统会自动退出之前投入的钱币，并且系统自动恢复到初始化状态等待再次使用。

<img src="http://image.kmoon.fun//images/202211032253293.png" alt="image-20221016205848571" style="zoom: 67%;" />

<center>图 1-1
<center>表 1-1


| 站数/站 | 票价/元 |
| :-----: | :-----: |
|  [1,5]  |    2    |
| [6,10]  |    3    |
| [11,15] |    4    |
| [16,20] |    5    |

## 二、设计方法

经过分析本自动售票机，其主要实现以下功能：出站点的选择、购票数量的选取、投币的处理、出票和余额找零的处理以及输入出错时的取消返回和恢复初始状态的处理。

- 整体功能：在购买车票时，乘客按“开始选择”按钮，然后选择出站口接着选择购票的数量（一次购票的票数限制在最多4张）；然后按“开始投币”按钮，开始投币；当投入钱币到达所需钱币时系统会自动打开出票口进行出票。如果需要找零此时系统会自动找零，在找零时全部以1元硬币形式给出。
- 取消购票：乘客进行购票操作在未出票前，乘客按取消按钮进行取消购票，此时系统会自动退回投入钱币并恢复系统等待下次操作，取消系统会回到原始等待购票状态。
- 票价计算：乘客购票只能选择一个出站口，根据其选择的出站口来计算所需要的票价。本设计全程共有27个站点，本设计起始站为**汉西一路**，乘客最多可以乘坐18站到达**滠口新城**。总共27站使用5位二进制来表示，即1站五环大道用00001表示，27站滠口新城用11011表示，起始站汉西一路用01000表示。
- 投币设计：本设计中售票机投币口只可以识别1元、10元的人民币。采用4位二进制表示，1元使用0001表示，10元采用1010表示。

## 三、程序流程图

首先当对自动售票机无操作时要在初始化状态下，即为等待购票时售票机的状
态，在初始化状态下按下“开始购票”后系统开始进入购票模式。在此模式下用户可使
用所有的购票功能。接着必须要选择出站口，再选择购票数。系统会根据选择的站点和购票数来判定所需要的投币金额，根据系统提示所需的投币金额进行投币。当投入的钱币达到所需的数目时,系统会自动从出票口出票，并将投入的钱币入库。当投入金额大于所需金额时，系统会自动出票并及时找零。购票过程中若有选择错误，可选择取消。如：在选择目的地、选择购票数时出错，可选择取消来恢复到初始化状态；当在投入钱币但不到所需金额时如有特殊情况可选择取消，系统会自动退回投入的钱币并恢复到初始化状态等待下一次购票。具体的购票流程图如图2-1。

<img src="http://image.kmoon.fun//images/202211091042844.png" alt="image-20221109104230719" style="zoom: 67%;" />

## 四、核心代码

```vhdl
LIBRARY IEEE;
USE IEEE.std_logic_1164.ALL;
USE IEEE.std_logic_unsigned.ALL;

entity subway is
port(
	clk:in std_logic;	--系统时钟
	select_start:in std_logic;	--"开始选择"按钮
	insert_start:in std_logic; --"开始投币"按钮
	money:in std_logic_vector(3 downto 0);	--投币入口
	cancel:in std_logic;	--"取消"按钮
	station:in std_logic_vector(4 downto 0);	--出站口编号
	numbers:in std_logic_vector(2 downto 0);	--购买张数
	ticket_num:out std_logic_vector(2 downto 0);	--出票数
	change_num:out std_logic_vector(4 downto 0);	--找零数
	ticket_gate:out std_logic;	--出票口
	change_gate:out std_logic	--找零口
);
end subway;

architecture bhv of subway is
	type state_type is(init_state,select_state,insert_state,ticket_state,change_state);	--状态变量申明
	signal state:state_type; 	--状态信号申明
	begin
		process(clk)
			variable type_exit:std_logic;--记录是否已选择出站口的变量
			variable type_out:std_logic;--记录是否已出票的变量
			variable type_num:std_logic;--记录是否已选择购票张数的变量
			variable price:std_logic_vector(4 downto 0);--记录单张票价的变量
			variable temp_number:std_logic_vector(2 downto 0);--记录票数的变量
			variable total_price:std_logic_vector(4 downto 0);	--记录总票价的变量
			variable total_insert:std_logic_vector(4 downto 0);	--记录投入钱币总额的变量
			variable change:std_logic_vector(4 downto 0);	--记录应找零金额的变量
			variable sign:std_logic;	--记录系统是否已经过初始化的变量
			variable temp_enough:std_logic;	--记录投入钱币金额达到总票价的变量
			variable x:std_logic;		--找零波形辅助变量
			variable y:std_logic;		--出票波形辅助变量
			variable distance:std_logic_vector(4 downto 0);	--记录出站口的中间变量
			variable num:std_logic_vector(2 downto 0);--记录票数的变量

			begin
				if(clk'event and clk='1') then	--时钟信号上升沿触发
					case state is
						when init_state=>	--初始状态
							if(sign='0')	then	--表示系统未经过初始化
								ticket_num<="000";	--购票记录清零
								change_num<="00000";	--找零数清零
								ticket_gate<='0';	--出票口关闭
								change_gate<='0';	--找零口关闭
								type_exit:='0';
								type_out:='0';
								price:="00000";	--单张票价记录清零
								type_num:='0';
								temp_number:="000";	--购票张数记录清零
								total_price:="00000";	--票价总额记录清零
								total_insert:="00000";	--投入钱币总额记录清零
								change:="00000";	--应找零金额记录清零
								sign:='1';	--记录已完成系统初始化
								temp_enough:='0';	--投入达到总票价记录清零
								distance:="00000";
								x:='0';
								y:='0';
								num:="000";
							else	--表示系统已经过初始化
								if(select_start='1') then --按下“开始选择”按钮
									sign:='0';
									state<=select_state;	--系统进入选择状态
								end if;
							end if;
						when select_state=>	--选择状态
							if(type_exit='0')	then	--表示尚未选择出站口
								distance:=station;
								if((distance>="00001")and(distance<="00011"))then
									price:="00011";	--票价为3元
									type_exit:='1';	--记录已选择出站口
								elsif((distance>="00100")and(distance<="01001"))then
									price:="00010";	--票价为2元
									type_exit:='1';
								elsif((distance>="01010")and(distance<="01110"))then
									price:="00010";	--票价为2元
									type_exit:='1';	
								elsif((distance>="01111")and(distance<="10011"))then
									price:="00011";	--票价为3元
									type_exit:='1';	
								elsif((distance>="10100")and(distance<="11000"))then
									price:="00100";	--票价为4元
									type_exit:='1';	
								elsif((distance>="11001")and(distance<="11011"))then
									price:="00101";	--票价为5元
									type_exit:='1';
								end if;
							end if;
							if((type_num='0')and(type_exit='1'))	then	--表示已选择出站口但尚未选择购票张数
								case numbers is
									when "100"=>	--选择4张票
										temp_number:="100";--购票张数为4
										num:="100";
										total_price:=price+price+price+price;--计算总票价
										type_num:='1';--记录已选择购票张数
									when "011"=>	--选择3张票
										temp_number:="011";
										num:="011";
										total_price:=price+price+price;
										type_num:='1';
									when "010"=>	--选择2张票
										temp_number:="010";
										num:="010";
										total_price:=price+price;
										type_num:='1';
									when "001"=>	--选择1张票
										temp_number:="001";
										num:="001";
										total_price:=price;
										type_num:='1';
									when others=>
										null;
								end case;
							end if;
							if((type_exit='1')and(type_num='1'))	then	--表示已选择出站口且已选择购票张数
								if(insert_start='1') then	--按下“开始投币”按钮
									state<=insert_state;--系统进入投币状态
								end if;
							end if;
							if(cancel='1') then --按下“取消”按钮
								state<=init_state;	--回到初始状态
							end if;
						when insert_state=>	--投币状态
								case money is	--投币
									when "0001"=>	--1元币
										total_insert:=total_insert+"00001";	--重新计算投币总额
										if(cancel='1') then	--按下“取消”按钮
											change:=total_insert;
											state<=change_state;	--回到初始状态
										end if;
									when "1010"=>  --10元币
										total_insert:=total_insert+"01010";	
										if(cancel='1') then	--按下“取消”按钮
											change:=total_insert;
											state<=change_state;	--回到初始状态
										end if;
									when others=>
										null;
								end case;
							
							if(total_insert>=total_price)then	--判断投入总金额是否已达到票价总额
								temp_enough:='1';--记录投入钱币金额达到总票价
							end if;
							if(temp_enough='1' and cancel='0')then	--判断投入钱币金额达到总票价
								state<=ticket_state;--系统进入出票状态
							end if;
						when ticket_state=>	--出票状态 
							ticket_num<=temp_number;--计算输出车票
							if(temp_number>"000" and y='0') then
								ticket_gate<='1';	--出票口打开
								temp_number:=temp_number-"001";--计算剩余票数
								y:='1';
							elsif(temp_number="000") then
								ticket_gate<='0';
								type_out:='0';
								if((type_out<='0') and (change>"00000")) then	--判断是否还有找零
									state<=change_state;	--系统进入找零状态
								else
									state<=init_state;	--找零完成，回到初始状态
								end if;
							else
								ticket_gate<='0';	
								y:='0';				--出票口关闭
							end if;
							if(num="100")	then
								change:=total_insert-price-price-price-price;
							elsif(num="011") then
								change:=total_insert-price-price-price;
							elsif(num="010") then
								change:=total_insert-price-price;
							elsif(num="001") then
								change:=total_insert-price;
							end if;
						when change_state=>	--找零状态	--找零口打开时，可控制每个时钟上升沿推出一个1元的硬币
							change_num<=change;
							if(change>"00000" and x='0') then
								change_gate<='1';		--找零口打开
								change:=change-"00001";		--计算剩余应找零金额
								x:='1';
							elsif(change="00000") then
								change_gate<='0';
								state<=init_state;	--系统回到初始状态
							else
								change_gate<='0';	
								x:='0';--找零口关闭
							end if;
				end case;
			end if;
	end process;
end bhv;
```



## 五、波形仿真

1、乘客按下开始按钮，进入选站台模式，选择二号站台，按下确定键，再选择票数为2张，按下确定键，售票机钱箱关闭，投入一张两元和五元纸币（对顺序没有要求），此时钱币总额大于票价，出两张票并找零一元。之后系统进入初始化状态。具体仿真如图 1 仿真1

图 1 仿真12、测试cancel键，当乘客按正确的操作完成选站台时，按下取消键，再重新选择，如图 2 cancel仿真，仿真波形如下。

图 2 cancel仿真

3、还是测试cancel键，当乘客选择好票数时，按下cancel键，然后重新选择两张单价为七块钱的六号站台票，投入一张20元和5元，找零六元。仿真波形如图 3 cancel仿真2

图 3 cancel仿真24、乘客选择五号站台，两张票，然后先后投入一元纸币，两元纸币，一元纸币，五元纸币，然后按下取消键，售票机自动放出所有的钱币。仿真如图 4 cancel仿真3。

图 4 cancel仿真3

## 六、心得体会

还有，乘客选择的站台以及票数，在售票机内部会自动将这两个信号传给出票系统，从而自动出票，以上写的程序只是让系统知道怎样收钱以及找零。

这次实验总体上来说比六人抢答器简单，但是因为这个售票机完全是自己写的，所以也不是想象中的那么简单。这也让我看出，要完全自己去做一件东西不是简单的，特别是要考虑很全面，还是要发一些时间的。

## 七、附录

### 1. 原理图

<img src="http://image.kmoon.fun//images/202211091102801.png" alt="image-20221109110225729" style="zoom:50%;" />

### 2. 源代码